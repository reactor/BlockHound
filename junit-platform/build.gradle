plugins {
    id "java"
    id "maven-publish"
}

description = "BlockHound JUnit Platform Integration"

sourceCompatibility = targetCompatibility = 8

repositories {
    jcenter()
}

test {
    useJUnitPlatform()

    forkEvery = 1
    maxParallelForks = 1
}

dependencies {
    compileOnly 'com.google.auto.service:auto-service:1.0-rc6'
    compile project(path: ":agent", configuration: 'shadow')

    compileOnly 'org.junit.platform:junit-platform-launcher:1.0.0'

    testCompile 'org.assertj:assertj-core:3.15.0'
    testCompile 'io.projectreactor:reactor-core:3.2.2.RELEASE'

    testCompile 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.6.2'

    // FIXME remove once https://github.com/gradle/gradle/issues/8806 is fixed
    testRuntime 'org.junit.platform:junit-platform-launcher:1.0.0'
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

jar {
  manifest {
    attributes 'Automatic-Module-Name': 'reactor.blockhound.junit'
  }
}

publishing {
    publications {
        mavenJava(MavenPublication) { publication ->
            from components.java
            artifact sourcesJar
            artifact javadocJar

            artifactId 'blockhound-junit-platform'
        }
    }
}

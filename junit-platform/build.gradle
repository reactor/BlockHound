plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
}

description = "BlockHound JUnit Platform Integration"
ext.detailedDescription = "Integrates the BlockHound Java agent to detect blocking calls in JUnit Platform-based tests."

sourceCompatibility = targetCompatibility = 8

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()

    forkEvery = 1
    maxParallelForks = 1
}

dependencies {
    compileOnly 'com.google.auto.service:auto-service-annotations:1.0.1'
    annotationProcessor 'com.google.auto.service:auto-service:1.0'

    //the api configuration ensures we publish blockhound at compile scope in pom
    api project(path: ":agent", configuration: 'shadow')

    compileOnly 'org.junit.platform:junit-platform-launcher:1.0.0'

    testImplementation 'org.assertj:assertj-core:3.21.0'
    testImplementation 'io.projectreactor:reactor-core:3.2.2.RELEASE'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    // FIXME remove once https://github.com/gradle/gradle/issues/8806 is fixed
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.0.0'
}

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
}

jar {
  manifest {
    attributes 'Automatic-Module-Name': 'reactor.blockhound.junit'
  }
}

publishing {
    publications {
        mavenJava(MavenPublication) { publication ->
            from components.java
            artifact sourcesJar
            artifact javadocJar

            artifactId 'blockhound-junit-platform'
        }
    }
}

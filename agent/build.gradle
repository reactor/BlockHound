plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.3'
    id "java"
    id "maven-publish"
}

sourceCompatibility = targetCompatibility = 8

shadowJar {
    classifier = null

    dependsOn(tasks.getByPath(":native-agent:externalBuildBlockHoundLinuxSharedLibrary"))
    from "${rootProject.project(":native-agent").buildDir}/external-build/BlockHound/linux/libBlockHound.so"

    dependsOn(tasks.getByPath(":native-agent:externalBuildBlockHoundOsxSharedLibrary"))
    from "${rootProject.project(":native-agent").buildDir}/external-build/BlockHound/osx/libBlockHound.dylib"

    dependsOn(tasks.getByPath(":native-agent:externalBuildBlockHoundWindowsSharedLibrary"))
    from "${rootProject.project(":native-agent").buildDir}/external-build/BlockHound/windows/libBlockHound.dll"

    manifest {
        attributes('Can-Retransform-Classes': 'true')
        attributes('Can-Set-Native-Method-Prefix': 'true')
        attributes('Can-Redefine-Classes': 'true')
    }

    exclude 'module-info.class'
    exclude 'META-INF/maven/**'
    exclude 'reactor/shaded/META-INF/**'
}

project.tasks.build.dependsOn(shadowJar)

task relocateShadowJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "reactor.shaded"
}

tasks.shadowJar.dependsOn tasks.relocateShadowJar

repositories {
    jcenter()
}

dependencies {
    compileOnly 'com.google.auto.service:auto-service:1.0-rc4'

    compile 'org.ow2.asm:asm:7.1'
    compile 'org.ow2.asm:asm-commons:7.1', {
        exclude(group: "org.ow2.asm", module: "*")
    }
    compile 'net.bytebuddy:byte-buddy-agent:1.9.10'

    compileOnly 'io.projectreactor:reactor-core:3.2.5.RELEASE'
    compileOnly 'io.reactivex.rxjava2:rxjava:2.2.5'
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) { publication ->
            artifacts.removeAll { it.classifier == null }
            artifact project.tasks.shadowJar
            artifact sourcesJar
            artifact javadocJar

            artifactId 'blockhound'
        }
    }
}
